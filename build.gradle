allprojects {
    group = 'ru.clevertec.banking'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        set('springBootVersion', "3.2.0")
        set('springDependencyManagementVersion', "1.1.4")
        set('lombokVersion', "1.18.30")
        set('mapstructVersion', "1.5.5.Final")
        set('lombokMapstructVersion', "0.2.0")
        set('preLiquibaseStarterVersion', "1.5.0")
        set('springCloudVersion', "2023.0.0")
        set('testContainersVersion','1.19.3')
        set('wiremockVersion','3.3.1')
        set('openapiStarterVersion',  '2.3.0')

    }

}

tasks.register('publishStarters') {
    it.finalizedBy {
        subprojects.findAll { it.name.endsWith('-starter') }
                .stream().map { subproject ->
                    subproject.tasks.named('publishToMavenLocal').get()
                }.toArray()
    }
    group 'custom_build'
}

tasks.register('publishStartersAndBootJarInServices') {
    it.finalizedBy {
        subprojects.findAll() {
            !it.name.endsWith('-starter') && it.tasks.findByName('bootJar') != null
        }.stream().map {
                    it.tasks.named('bootJar').get()
                }.toArray()


    }
    dependsOn('publishStarters')
    group 'custom_build'
}

